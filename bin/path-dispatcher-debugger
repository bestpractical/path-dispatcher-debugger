#!/usr/bin/env perl
use strict;
use warnings;
use Path::Dispatcher::Debugger;
use File::ShareDir 'dist_dir';
use Plack::Request;
use Plack::Builder;
use Scalar::Util 'blessed';

my $dispatcher = $ENV{DEBUG_DISPATCHER} or die "Please set environment variable DEBUG_DISPATCHER";
if ($dispatcher =~ /^{/) {
    $dispatcher = eval $dispatcher;
    die $@ if $@;
}
else {
    Any::Moose::load_class($dispatcher);
}

if ($dispatcher->isa('Path::Dispatcher::Declarative')) {
    $dispatcher = $dispatcher->dispatcher;
}

my $port = 9187;
my $debugger = Path::Dispatcher::Debugger->new(
    dispatcher => $dispatcher,
);

Template::Declare->init(dispatch_to => [$debugger->view_class]);

my $app = sub {
    my $request = Plack::Request->new(shift);

    if (Template::Declare->has_template($request->request_uri)) {
        my $body = Template::Declare->show($request->request_uri, $debugger);

        my $response = $request->new_response(200);
        $response->content_type('text/html');
        $response->body($body);
        return $response->finalize;
    }

    return [404, ['Content-Type' => 'text/plain'], ['not found']];
};

builder {
    mount "/static" => builder {
        require Plack::App::File;
        Plack::App::File->new(root => dist_dir('Path-Dispatcher-Debugger'))->to_app;
    };
    mount "/" => $app;
};

